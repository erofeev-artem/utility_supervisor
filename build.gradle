import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.palantir.git-version' version "3.3.0"
    id 'com.bmuschko.docker-remote-api' version "9.4.0"
}

group = 'org.monkey_business'
version = gitVersion()

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('dockerBuild', DockerBuildImage) {
    dependsOn 'build'
    inputDir = file('.')
    images.add("utility_supervisor:${version}")
    doFirst{
        println "Docker version: ${'docker --version'.execute().text}"
    }
}


tasks.register('dockerTag', DockerTagImage){
    dependsOn dockerBuild
    repository = "87.228.99.141:5000/utility_supervisor"
    tag = version
    imageId = dockerBuild.getImageId()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.jsoup:jsoup:1.18.3'
    implementation 'org.telegram:telegrambots-spring-boot-starter:6.9.7.1'
    implementation 'com.bucket4j:bucket4j-core:8.7.0'
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'io.mongock:mongock-springboot:5.5.0'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}



